;// A = DPM
;
;// /INDENT = number of spaces to indent a line
;
@INIT,
IF{@Memory.store.file(/MH,/PATH) /PATH_" cannot be opened/edited!"^ERR;
   @COMPILE,
   @SAVE},
ERR;

INIT
0^/INDENT,
A^DPM^NPR.DPM.dpm.code^NPR.SEG.ele.dpm,
A#"0."^APP^NPR.APPL.mnemonic,
"C:\Data Definitions"^ROOT,
ROOT@Directory.create,
ROOT_"\"_APP@Directory.create,
ROOT_"\"_APP_"\"_DPM_".xml"^/PATH,
""^ROOT^APP^DPM,
D(34)_"'<>&"^/CHARS,
{"&quot;","&apos;","&lt;","&gt;","&amp;"}^/ESCAPE,
@Memory.allocate("")^/MH

SAVE
IF{@Memory.store.file(/MH,/PATH) "Couldn't write data!"^ERR},
@Memory.free(/MH)

; #####################
; ##### Utilities #####
; #####################

#OPEN
;//Opens a tag.
" ":/INDENT@OUT,
@Add(2,/INDENT),
"<"_A_">"@OUT,
@EOL@OUT;

#CLOSE
;//Closes a tag.
@Sub(2,/INDENT),
" ":/INDENT@OUT,
"</"_A_">"@OUT,
@EOL@OUT;

#TAG
; A = Name
; B = Value
B^VAL,
" ":/INDENT@OUT,
IF{'VAL "<"_A_"/>"@OUT,
      @EOL@OUT;
   "<"_A_">"@OUT,
   @SEND.VALUE,
   "</"_A_">"@OUT,
   @EOL@OUT},
END;

#MULTI.TAG
; A = Name
; B = Root of structure to loop on
" ":/INDENT@OUT,
"<"_A@OUT,
IF{+[B,""]_.=. "/>"@OUT;
   ">"@OUT,
   DO{+([B,C],VAL)^C @SEND.VALUE},
   "</"_A_">"@OUT},
@EOL@OUT;

SEND.VALUE
L(VAL)^Z,0^X,
DO{X<Z IF{L(/CHARS,VAL#X)^Y=L(/CHARS) VAL#X@OUT;
          /ESCAPE|Y@OUT},
          @Add(1,X)}

OUT
^@Memory.write(/MH),
@Memory.store.file(/MH,/PATH)

EOL
D(13,10)

; #####################
; ## Compile Section ##
; #####################

COMPILE
@OPEN("segments"),
DO{@Next(NPR.SEG.name) @SEGMENT},
@CLOSE("segments")

; #####################
; ## Segment Section ##
; #####################

SEGMENT
@OPEN("segment"),
@TAG("name",NPR.SEG.name),
@TAG("physical",@NPR.SEG.phy.base),
@TAG("value",@NPR.SEG.phy.val.ele),
@SEGMENT.CHILDREN,
@SEGMENT.SUBSCRIPTS,
@SEGMENT.ELEMENTS,
@CLOSE("segment")

SEGMENT.CHILDREN
@OPEN("children"),
DO{@Next(NPR.SEG.child.segment) @TAG("child",NPR.SEG.child.segment)},
@CLOSE("children")

SEGMENT.SUBSCRIPTS
@OPEN("subscripts"),
DO{@Next(NPR.SEG.sub.urn) @TAG("subscript",@NPR.SEG.sub.element)},
@CLOSE("subscripts")

SEGMENT.ELEMENTS
@OPEN("elements"),
DO{@Next.get(NPR.SEG.sort.element,NPR.SEG.ele.name) @ELEMENT},
@CLOSE("elements")

; #####################
; ## Element Section ##
; #####################

ELEMENT
@OPEN("element"),
@TAG("name",NPR.SEG.ele.name),
@TAG("local",@NPR.SEG.ele.local),
@TAG("physical",@NPR.SEG.ele.phy.address),
@TAG("segment",@NPR.SEG.ele.seg),
@TAG("pointer",@NPR.SEG.ele.pointer),
@TAG("type",@NPR.SEG.ele.data.type),
@TAG("offset",@NPR.SEG.ele.offset),
@TAG("length",@NPR.SEG.ele.length),
@COMPILE.ATTRIBUTES,@MULTI.TAG("attributes",^/ATTRIB),
@MULTI.TAG("description",^@Root(@NPR.SEG.description)),
@MULTI.TAG("documentation",^@Root(@NPR.SEG.ele.tech.doc)),
@CLOSE("element")

COMPILE.ATTRIBUTES
K(/ATTRIB),
""^Q,
@Last(NPR.SEG.ele.attribute.index)^L,
DO{@Next.get(NPR.SEG.ele.attribute.index,Y)^X @Add(1,Q),
                                              X_"="_Y_IF{X=L "";
                                                         @EOL}^/ATTRIB[Q]}
